# Copyright (c) Joby Aviation 2022
# Original authors: Thulio Ferraz Assis (thulio@aspect.dev), Aspect.dev
#
# Copyright (c) Thulio Ferraz Assis 2024
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

build_test(
    name = "openssl_build_test",
    targets = [
        ":openssl",
        ":openssl_linux_aarch64",
        ":openssl_linux_armv7",
    ],
)

configure_make(
    name = "openssl",
    configure_command = "Configure",
    configure_in_place = True,
    configure_options = select({
        "//platforms/config:aarch64_linux": ["linux-aarch64"],
        "//platforms/config:armv7_linux": ["linux-generic32"],
        "//platforms/config:x86_64_linux": ["linux-generic64"],
    }) + [
        "no-comp",
        "no-idea",
        "no-weak-ssl-ciphers",
        "no-shared",
    ],
    lib_name = "openssl",
    lib_source = "@openssl//:srcs",
    out_static_libs = [
        "libssl.a",
        "libcrypto.a",
    ],
    targets = [
        "install_sw",
        "install_ssldirs",
    ],
    visibility = ["//visibility:public"],
)

platform_transition_filegroup(
    name = "openssl_linux_aarch64",
    srcs = [":openssl"],
    target_platform = "//platforms:aarch64_linux",
)

platform_transition_filegroup(
    name = "openssl_linux_armv7",
    srcs = [":openssl"],
    target_platform = "//platforms:armv7_linux",
)
